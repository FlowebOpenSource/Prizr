package fi.floweb
 
import fi.floweb.prizr.beans.*;

rule "Category, country and location code determine which pricemultiplier should be used"
	salience 1
	when 
		$preq : PricingRequest()
		$pres : PricingResponse(pricingDone == false)
		$multiplier : MultiplierBase((appliesToLocation == $preq.locationCode || appliesToLocation == "Kaikki") && 
									countryCode == $preq.countryCode &&
									(appliesToCategory == $preq.itemCategoryCode || appliesToCategory == "Kaikki") &&
									(appliesToShopCode == $preq.shopCode || appliesToShopCode == "Kaikki"))
	then
		double newPrice = $preq.getSalesPrice()*$preq.getCommission()*$multiplier.getMultiplier();
		double transit = $preq.getItemStandardCost()+($multiplier.getIncludesFreight() / $preq.getItemUnitCC());
		transit = transit * $multiplier.getFreightMultiplier();
		double finalPrice = newPrice + transit; 
		$pres.setPrice(finalPrice);
		$pres.setPricingDone(true);
		update( $pres )
end

rule "If there are no CC and standard cost in request, calculate freight costs by baseFreightMultiplier"
	salience 2
	when 
		$preq : PricingRequest(itemStandardCost == 0 || itemUnitCC == 0);
		$pres : PricingResponse(pricingDone == false)
		$multiplier : MultiplierBase(appliesToLocation == $preq.locationCode && 
									countryCode == $preq.countryCode &&
									appliesToCategory == $preq.itemCategoryCode &&
									(appliesToShopCode == $preq.shopCode || appliesToShopCode == "Kaikki"))
	then
		double newPrice = $preq.getSalesPrice()*$preq.getCommission()*$multiplier.getMultiplier();
		double transit = $preq.getSalesPrice()*$multiplier.getBaseFreightMultiplier();
		double finalPrice = newPrice + transit; 
		$pres.setPrice(finalPrice);
		$pres.setPricingDone(true);
		update( $pres )
end

rule "If commission is zero (as it's a multiplier), it is interpreted as having no effect (one)"
	salience 3
	when
		$preq : PricingRequest(commission == 0); 
		$pres : PricingResponse(pricingDone == false);
	then
		$preq.setCommission(1);
		update( $preq );
end
